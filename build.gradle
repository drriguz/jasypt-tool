plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "de.jjohannes.extra-java-module-info" version "0.12"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.beryx.jlink' version '2.25.0'
}

application {
    mainClassName = 'com.riguz.jasypt.App'
}

javafx {
    version = "17.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'jasypttool'
    }
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}

group 'com.riguz.jasypt'
version '1.0'

repositories {
    mavenCentral()
}

extraJavaModuleInfo {
    automaticModule("org.jasypt:jasypt", "org.jasypt")
    automaticModule("org.passay:passay", "org.passay")
}

dependencies {
    implementation 'org.jasypt:jasypt:1.9.3'
    implementation 'org.passay:passay:1.6.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}